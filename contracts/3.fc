{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

int equal_slices?(slice a, slice b) asm "SDEQ";
int bitsize(int n) asm "BITSIZE";

() recv_internal() {
}

builder replace(slice search, slice replacement, slice source, builder result) {
  if (source.slice_bits() == 0) {
    if (source.slice_refs() > 0) {
      return replace(search, replacement, source~load_ref().begin_parse(), result);
    } else {
      return result;
    }
  }

  slice chunk = "";
  slice paste = "";
  slice next = source.slice_refs() ? source.preload_ref().begin_parse() : null();
  int from_next = 0;

  if (source.slice_bits() >= search.slice_bits()) {
    chunk = source.preload_bits(search.slice_bits());
  } else {
    if (~ null?(next)) {
      from_next = min(search.slice_bits() - source.slice_bits(), next.slice_bits());
      chunk = begin_cell()
              .store_slice(source)
              .store_slice(next.preload_bits(from_next))
              .end_cell()
              .begin_parse();
    }
  }

  if (equal_slices?(chunk, search)) {
    paste = replacement;
    if (from_next) {
      source = next;
      source~load_bits(from_next);
    } else {
      source~load_bits(search.slice_bits());
    }
  } else {
    paste = source~load_bits(1);
  }

  if (paste.slice_bits() <= 1023 - result.builder_bits()) {
    result = result.store_slice(paste);
    return replace(search, replacement, source, result);
  }
  slice first_part = paste~load_bits((1023 - result.builder_bits()));
  result = result.store_slice(first_part);
  return result.store_ref(
          replace(search, replacement, source, begin_cell().store_slice(paste)).end_cell()
  );
}

;; testable
cell find_and_replace(int flag, int value, cell linked_list) method_id {
  slice source = linked_list.begin_parse();
  builder result = begin_cell();

  slice search = begin_cell().store_uint(flag, bitsize(flag) - 1).end_cell().begin_parse();
  slice replacement = begin_cell().store_uint(value, bitsize(value) - 1).end_cell().begin_parse();

  return replace(search, replacement, source, result).end_cell();
}

