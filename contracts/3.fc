{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

int bitsize(int n) asm "BITSIZE";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

() recv_internal() {
}

(int) binpow (int n, int e) {
  if (e == 0) {
    return 1;
  }
  if (e == 1) {
    return n;
  }
  int p = binpow(n, e / 2);
  p *= p;
  if ((e % 2) == 1) {
    p *= n;
  }
  return p;
}

;; testable
cell find_and_replace(int flag, int value, cell linked_list) method_id {
  slice current = linked_list.begin_parse();
  int flag_size = bitsize(flag) - 1;
  int value_size = bitsize(value) - 1;

  builder result = begin_cell();
  tuple result_stack = null();

  do {
    slice next = current.slice_refs() ? current~load_ref().begin_parse() : null();
    while (current.slice_bits()) {
      int from_current = min(128, min(current.slice_bits(), flag_size));
      int from_next = next.null?() ? 0 : min(next.slice_bits(), flag_size - from_current);

      int fragment = current.preload_uint(from_current) * binpow(2, from_next) + (from_next ? next.preload_uint(from_next) : 0);

      if (fragment == flag) {
        int to_current = 1023 - result.builder_bits() >= value_size ? value_size : 1023 - result.builder_bits();
        int to_next = value_size - to_current;

        slice paste = begin_cell().store_uint(value, value_size).end_cell().begin_parse();


        if (to_current) {
          result = result.store_slice(paste~load_bits(to_current));
        }
        if (to_next) {
          result_stack~push_back(result);
          result = begin_cell().store_slice(paste);
        }
        if (from_current) {
          current~load_bits(from_current);
        }
        if (from_next) {
          next~load_bits(from_next);
        }
      } else {
        if (1023 - result.builder_bits()) {
          result = result.store_slice(current~load_bits(1));
        } else {
          result_stack~push_back(result);
          result = begin_cell().store_slice(current~load_bits(1));
        }
      }
    }
    current = next;
  } until (current.null?());

  while (~ result_stack.null?()) {
    builder part = result_stack~pop_back();
    result = part.store_ref(result.end_cell());
  }

  return result.end_cell();
}
