{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

int bitsize(int n) asm "BITSIZE";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";
int pow2(int n) asm "POW2";

() recv_internal() {
}

;; testable
cell find_and_replace(int flag, int value, cell linked_list) method_id {
  slice current = linked_list.begin_parse();
  int flag_size = bitsize(flag) - 1;
  int value_size = bitsize(value) - 1;

  slice value_slice = begin_cell().store_uint(value, value_size).end_cell().begin_parse();

  builder result = begin_cell();
  tuple result_stack = null();

  do {
    slice next = current.slice_refs() ? current~load_ref().begin_parse() : null();
    while (current.slice_bits()) {
      int from_current = min(current.slice_bits(), flag_size);
      int from_next = next.null?() ? 0 : min(next.slice_bits(), flag_size - from_current);

      int fragment = current.preload_uint(from_current) * pow2(from_next) + (from_next ? next.preload_uint(from_next) : 0);

      if (fragment == flag) {
        if (1023 - result.builder_bits() >= value_size) {
          result = result.store_uint(value, value_size);
        } else {
          if (result.builder_bits() == 1023) {
            result_stack~push_back(result);
            result = begin_cell().store_uint(value, value_size);
          } else {
            slice paste = value_slice;
            result_stack~push_back(result.store_slice(paste~load_bits(1023 - result.builder_bits())));
            result = begin_cell().store_slice(paste);
          }
        }

        if (from_current) {
          current~skip_bits(from_current);
        }
        if (from_next) {
          next~skip_bits(from_next);
        }
      } else {
        if (1023 - result.builder_bits()) {
          result = result.store_slice(current~load_bits(1));
        } else {
          result_stack~push_back(result);
          result = begin_cell().store_slice(current~load_bits(1));
        }
      }
    }
    current = next;
  } until (current.null?());

  while (~ result_stack.null?()) {
    builder part = result_stack~pop_back();
    result = part.store_ref(result.end_cell());
  }

  return result.end_cell();
}
