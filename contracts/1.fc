forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  if (tree.cell_hash() == hash) {
    return tree;
  }
  slice tree_slice = tree.begin_parse();
  repeat (tree_slice.slice_refs()) {
    cell branch = tree_slice~load_ref();
    cell result = find_branch_by_hash(hash, branch);
    if (~ result.is_null()) {
      return result;
    }
  }

  return null();
}

cell get_branch_by_hash(int hash, cell tree) method_id {
  cell result = find_branch_by_hash(hash, tree);
  return result.is_null() ? begin_cell().end_cell() : result;
}